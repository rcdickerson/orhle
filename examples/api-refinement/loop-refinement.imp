// A refinement involving a simple loop.

expected: valid;

forall: refinement;
exists: original;

post: (= original!sum refinement!sum);

aspecs:
  randOdd() {
    post: (and (<= 0 ret!) (< ret! 10) (= (mod ret! 2) 1));
  }

especs:
  rand() {
    templateVars: n;
    pre:  (and (<= 0 n) (< n 10));
    post: (= ret! n);
  }

prog original():
  sum := 0;
  while (sum <= 100) do
    @inv { (and (=> (<= original!sum 100) (and (< 100 refinement!sum) (< refinement!sum 110)))
                (=> (< 100 original!sum) (= original!sum refinement!sum))) }
    @var { (- 110 sum) }
    r := call rand();
    sum := sum + r;
  end
endp

prog refinement():
  sum := 0;
  while (sum <= 100) do
    @inv { (and (<= 0 refinement!sum) (< refinement!sum 110)) }
    r := call randOdd();
    sum := sum + r;
  end
endp