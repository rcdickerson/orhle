// From:
//   Sabelfeld, Andrei & Myers, Andrew. (2004).
//   A Model for Delimited Information Release.
//   Lecture Notes in Computer Science. 10.1007/978-3-540-37621-7_9.

expected: valid;

forall: buy[1], buy[2];
exists: buy[3];

// High:
//   funds
// Low:
//   spent
//   cost
// Delimited Release:
//   funds >= cost

pre: (and
       // High
       (= buy!1!funds buy!3!funds)

       // Low
       (= buy!1!spent buy!2!spent)
       (= buy!2!spent buy!3!spent)
       (= buy!1!cost buy!2!cost)
       (= buy!2!cost buy!3!cost)

       // Delimited Release
       (= (>= buy!1!funds buy!1!cost) (>= buy!2!funds buy!2!cost))
       (= (>= buy!2!funds buy!2!cost) (>= buy!3!funds buy!3!cost)));

post: (= buy!2!spent buy!3!spent);


prog buy(funds, spent, cost):
  if (funds >= cost) then
    funds := funds - cost;
    spent := spent + cost;
  else
    skip;
  end
endp