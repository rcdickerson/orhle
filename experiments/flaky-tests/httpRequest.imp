// Example from the IDoFT dataset:
// http://mir.cs.illinois.edu/flakytests/tsoiso.html

// CUT: https://github.com/CrawlScript/WebCollector/blob/016b61a1f6e52cf2ffb048a2abcc01d767beb0ab/src/main/java/cn/edu/hfut/dmic/webcollector/plugin/net/OkHttpRequester.java

// Test: https://github.com/CrawlScript/WebCollector/blob/016b61a1f6e52cf2ffb048a2abcc01d767beb0ab/src/test/java/cn/edu/hfut/dmic/webcollector/util/OkHttpRequesterTest.java

// Many tests in the IDoFT do something similar, e.g.:
// https://github.com/undertow-io/undertow/blob/d0efffad5d2034bb07525cac9b299dac72c3045d/core/src/test/java/io/undertow/server/handlers/proxy/AbstractLoadBalancingProxyTestCase.java


// First, assert the test is flaky:
exists: test[1], test[2];
pre:    true;
post:   (not (= test!1!success test!2!success));

// Alternately you could assert the test is *not* flaky:
// forall: test[1], test[2];
// pre:    true;
// post:   (= test!1!success test!2!success);

// Assume a service can respond with 200, 400, or 500.
aspecs:
  service(url) {
    pre: true;
    post: (or (= ret! 200)
              (= ret! 400)
              (= ret! 500));
  }

// Furthermore, assume all of these responses will actually
// happen at some point.
especs:
  service(url) {
    choiceVars: k;
    pre: (or (= k 0) (= k 1) (= k 3));
    post: (and
            (=> (= k 0) (= ret! 200))
            (=> (= k 1) (= ret! 400))
            (=> (= k 2) (= ret! 500)));
  }

// Code under test reports whether or not a call returns 200:
fun makeRequest(url) {
  response := call service(url);
  if response == 200 then
    result := 1;
  else
    result := 0;
  endif
  return result;
}

// The flaky test (behavior depends on service response):
fun test() {
  url := 1111;
  response := call makeRequest(url);
  if (response == 0) then
    success := 0;
  else
    success := 1;
  endif
  return success;
}